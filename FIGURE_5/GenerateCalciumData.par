
EGTA.total  = $2
BAPTA.total = $3
Rpatch      = $4
B.kappa     = $5
B.KD        = $6
B.kplus     = $7
tauCa       = $8
ICA         = $9
N           = $10
totalTime   = $11
tSteps      = $12
fName       = $13
           
Ca.source 0 0.001

verbose = 0

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  Geometry declarations %%%%%%%%%%%%%%%%%%%%%%%%%%%%

volume 0 Rpatch
geometry spherical
stretch.factor = 1.02
stretch r 0 0 
grid N

%%%%%%%%%%%%%%%%%%%%%%%%%%%%  Buffer and Ca2+ parameters  %%%%%%%%%%%%%%%%%%%%%%%%

Ca.D   = 0.22
Ca.bgr = 0.05
uptake = 1 / tauCa 

%bc.define Pump   1  0  0.04  1  0.2   
%Ca.bc Noflux Pump 

buffer ATP
ATP.D     = 0.2
ATP.total = 500
ATP.kplus = 0.15
ATP.KD    = 200

buffer B
B.D = 0.05
B.total = B.kappa * B.KD

if EGTA.total > 0
    buffer EGTA
    EGTA.D  = 0.2    % Naraghi '97 Cell Ca 22:255
    EGTA.KD = 0.174
    EGTA.kminus = 0.00047
endif

if BAPTA.total > 0
    buffer BAPTA
    BAPTA.D  = 0.2
    BAPTA.KD = 0.176
    BAPTA.kminus = 0.079
endif

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  Simulation protocol  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

run adaptive  totalTime
%run nonadaptive totalTime 0.00002

current := ICA (0.7 + 0.3 (0.56 exp(-t/5) + 0.44 exp(-t/100) ) ) pA

plot binary Ca fName
plot.steps.binary = tSteps







